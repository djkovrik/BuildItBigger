apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        free {
            applicationId 'com.udacity.gradle.builditbigger.free'
        }
        paid {
            applicationId 'com.udacity.gradle.builditbigger.paid'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    freeCompile 'com.google.android.gms:play-services-ads:10.2.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile 'com.google.api-client:google-api-client-android:1.19.0' exclude module: 'httpclient'
    compile 'com.google.http-client:google-http-client-gson:1.19.0' exclude module: 'httpclient'
    compile project(path: ':displayjoke')
    compile project(path: ':backend', configuration: 'android-endpoints')
}

// Testing tasks
task startAppEngine(dependsOn: ':backend:appengineRun') {
    doLast {
        println "---> Local GCE dev server started!"
    }
}

task runLocalGCETests(dependsOn: 'startAppEngine') {
    group "App Local GCE test"
    description "Starts the GCE dev server, runs all the Android tests, and shuts down the dev server."

    project(':backend').afterEvaluate { backend ->
        backend.extensions.appengine.daemon = true
    }

    finalizedBy {
        runTests
    }
}

task runTests(dependsOn: ':app:connectedAndroidTest') {
    doLast {
        println "---> Android testing finished!"
    }

    finalizedBy {
        stopAppEngine
    }
}

task stopAppEngine(dependsOn: ':backend:appengineStop') {
    doLast {
        println "---> Local GCE dev server stopped!"
    }
}